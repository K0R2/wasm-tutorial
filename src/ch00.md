# Introduction

Welcome to **A Tour of WebAssembly**, an introductory book about WebAssembly.

WebAssembly is a new language that can be run in the web browser. It is designed to be a high-performance, low-level language that can be used as a portable compilation target for high-level languages like C, C++, Rust, and others.

## Formats

Wasm code can be represented in both **binary format** and **textual format**. While binary format is designed for compact representation and efficient decoding, textual format enable Wasm code to be read and edited by humans. Web browsers load and run Wasm code in binary format.

## Modules

The fundamental unit of code in Wasm is a module. A module contains definitions for functions, tables, linear memories, as well as global variables. Web browsers load Wasm code by modules.

```wasm
(module)
```

This is the simplest Wasm module that is totally empty.

## Functions

Wasm code is organized into separate functions. Each function takes a sequence of values as parameters and returns a sequence of values as results. Functions can call each other, including recursively, resulting in an implicit call stack that cannot be accessed directly. Functions may also declare mutable local variables that are usable as virtual registers.

```wasm
(module
  ;; simply return a constant 42
  (func $answer (result i32)
    (i32.const 42)
  )
  ;; functions must be exported to be invoked by embedders
  (export "answer" (func $answer))
)

;; invoke exported function "answer"
;; note this command itself is not Wasm code
(invoke "answer") ;; 42 : i32
```

## Values

Wasm provides four basic number types:
 - `i32`: 32-bit integer
 - `i64`: 64-bit integer
 - `f32`: 32-bit float
 - `f64`: 64-bit float

Wasm is designed to be portable. There is no `f80` or `f128` even if your hardware support them.

```wasm
(module
  ;; functions can be exported inline
  (func (export "i32") (result i32)
    (i32.const 42)
  )
  (func (export "i64") (result i64)
    (i64.const 42)
  )
  (func (export "f32") (result f32)
    (f32.const 3.14159265358979323846264338327950)
  )
  (func (export "f64") (result f64)
    (f64.const 3.14159265358979323846264338327950)
  )
)

(invoke "i32") ;; 42 : i32
(invoke "i64") ;; 42 : i64
(invoke "f32") ;; 3.141_592_741_012_573 : f32
(invoke "f64") ;; 3.141_592_653_589_793 : f64
```

Values can also consist of other types, which we will introduce later in the book.

## Instructions

The computational model of Wasm is based on a stack machine. Wasm code consists of sequences of instructions that are executed in order. Instructions manipulate values on an implicit operand stack and fall into two main categories. Simple instructions perform basic operations on data. They pop arguments from the operand stack and push results back to it. Control instructions alter control flow.

> Note: In practice, web browsers need not maintain an actual operand stack. Instead, they can compile stack-based Wasm instructions to efficient register-based machine instructions.

```wasm
(module
  ;; return the sum of two parameters
  (func (export "sum") (param i32 i32) (result i32)
    (local.get 0) ;; push the 1st parameter to the stack
    (local.get 1) ;; push the 2nd parameter to the stack
    (i32.add) ;; pop 2 i32s from the stack and push their sum to the stack
  )
  ;; return the smaller and the greater of two parameters
  (func (export "minmax") (param f64 f64) (result f64 f64)
    ;; folded instructions are solely syntactic sugar
    (f64.min (local.get 0) (local.get 1)) ;; push the smaller parameter to the stack
    (f64.max (local.get 0) (local.get 1)) ;; push the greater parameter to the stack
  )
)

(invoke "sum" (i32.const 2) (i32.const 3)) ;; 5 : i32
(invoke "minmax" (f64.const 3.14159) (f64.const 2.71828)) ;; [2.718_28 3.141_59] : [f64 f64]
```

## Traps

Under some conditions, certain instructions may produce a trap, which immediately aborts execution. Traps cannot be handled by Wasm code, but are reported to the outside environment, where they typically can be caught.

```wasm
(module
  ;; divide 1 by 0 will produce a trap
  (func (export "trap") (result i32)
    (i32.div_u (i32.const 1) (i32.const 0))
  )
)

(invoke "trap") ;; runtime trap: integer divide by zero
```

## Assertions

In this book, we use assertions to describe the behavior of our code.

```wasm
(module
  (func (export "answer") (result i32)
    (i32.const 42)
  )
  (func (export "trap") (result i32)
    (i32.div_u (i32.const 1) (i32.const 0))
  )
)

;; note assertions themselves are not Wasm code
(assert_return (invoke "answer") (i32.const 42))
(assert_trap (invoke "trap") "integer divide by zero")
```

## Comments

A comment in Wasm textual format can either be a line comment started with a double semicolon `;;` and extending to the end of the line, or a block comment enclosed in delimiters `(;` and `;)`. Block comments can be nested.
